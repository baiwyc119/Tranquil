\ Tranquil compiler
import "stdlib"

error = { reason |
    "Error! «reason»" print
    Exit(1)
}

arch = "x86_64"

inPath  = nil
outPath = @"a.out"
args    = ... objectEnumerator

ldFlags        = [@-fexceptions, @-all_load,
                  @"-mmacosx-version-min=10.7",
                  @-lm, @-ldl, @-lobjc, @-lffi,
                  @-framework, @Foundation,
                  @"/usr/local/tranquil/lib/libtranquil.a",
                  @"/usr/local/tranquil/objfw/lib/libobjfw.a",
                  @"/usr/local/tranquil/gmp/lib/libgmp.a"]

while arg = args nextObject {
    if arg == @-o then
        outPath = args nextObject
    else if arg == @-arch then
        arch = args nextObject
    else if inPath == nil then
        inPath = arg
    else
        ldFlags push: arg
}
error("No input provided") unless inPath ~= nil


uuid = `NSProcessInfo processInfo globallyUniqueString`

irPath = "/tmp/«uuid()».bc"
status = NSTask launchedTaskWithLaunchPath: "/usr/local/tranquil/bin/tranquil"
                                 arguments: [@-aot, @-arch, arch, @-o, irPath, inPath];
                             waitUntilExit;
                         terminationStatus
error("Compilation failed") if status ~= 0

asmPath = "/tmp/«uuid()».s"
llcArchs = { @x86_64 => @x86-64, @i386 => @x86, @armv7 => @arm }
llcArchs print
"arch: «arch» => «llcArchs[arch]»" print
status = NSTask launchedTaskWithLaunchPath: "/usr/local/tranquil/llvm/bin/llc"
                                 arguments: [@-O3, @-disable-fp-elim, "-march=«llcArchs[arch]»", @-relocation-model=pic, @-o, asmPath, irPath];
                             waitUntilExit;
                         terminationStatus
error("Assembly failed") if status ~= 0

status = NSTask launchedTaskWithLaunchPath: "/usr/local/tranquil/llvm/bin/clang"
                                 arguments: ldFlags + [@-arch, arch, @-o, outPath, asmPath, @"/usr/lib/arc/libarclite_macosx.a"];
                             waitUntilExit;
                         terminationStatus
error("Linking failed") if status ~= 0
