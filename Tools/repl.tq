#!/usr/local/tranquil/bin/tranquil
import "readline/readline"
import "stdlib"

"welcome; this is tranquil." print

historyPath   = "/tmp/tqrepl_history"
\ Very naive heuristic to know whether to prepend a return operator to the line.
nonReturnable = `str | (str hasPrefix: "import "      ) || (str hasPrefix: "if "     ) ||
                       (str hasPrefix: "unless "      ) || (str hasPrefix: "while "  ) ||
                       (str hasPrefix: "until "       ) || (str hasPrefix: "async "  ) ||
                       (str hasPrefix: "whenFinished ") || (str matches: /=\s*async /) ||
                       ((str hasPrefix: "#")   &&  (str[0] == str[0] uppercaseString))`

History_truncate_file(historyPath, 300) \ Just to make sure things don't get unmanageable
Read_history(historyPath)

getInput = { indent = 0 |
    ptr = Readline((" " * indent * 4) + "> ")
    str = ptr toString
    if str length > 0 {
        Add_history(str)
        Write_history(historyPath)
    }
    Free(ptr)

    if str {
        indent += ((/\{/ matches: str) count) - ((/\}/ matches: str) count)
        ^(indent > 0) ? (str + "\n" + getInput(indent)) ! str
    }
}

while line = getInput() {
    line = "^«line»" unless nonReturnable(line)
    result = TQProgram sharedProgram executeScript: line error: nil
    if result then
        " -> «result» («result class»)" print
    else if line != "^" then
        " -> nil" print
}
