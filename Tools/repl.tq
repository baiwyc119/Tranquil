#!/usr/local/tranquil/bin/tranquil
import "readline/readline"
import "stdlib"

"welcome; this is tranquil." print

historyPath   = "/tmp/tqrepl_history"

History_truncate_file(historyPath, 300) \ Just to make sure things don't get unmanageable
Read_history(historyPath)

getInput = { indent = 0, shouldDecrement = no |
    ptr = Readline((" " * indent * 4) + "> ")
    str = ptr toString
    if str length > 0 {
        Add_history(str)
        Write_history(historyPath)
    }
    Free(ptr)

    if str {
        if shouldDecrement {
            shouldDecrement = no
            --indent
        }
        if /(then|else|\+|\*|\/|-|\^|:|;)\s*$/ matches: str {
            ++indent
            shouldDecrement = yes
        }
        indent += ((/\{/ matches: str) count) - ((/\}/ matches: str) count)
        ^(indent > 0) ? (str + "\n" + getInput(indent, shouldDecrement)) ! str
    }
}

while line = getInput() {
    if line length > 0 {
        result = TQProgram sharedProgram executeScript: line
                                               onError: `err| err localizedDescription print`
        " -> «result» («result class»)" print if result
    }
}
