#!/usr/local/tranquil/bin/tranquil
import "unistd"
import "stdlib"

error = { msg |
    msg print
    Exit(1)
}

`error("No input provided")` if: ... count == 0
scriptPath   = ...[0]
task         = nil
lastModified = nil

modificationDate = `(NSFileManager defaultManager attributesOfItemAtPath:scriptPath error:nil)[@NSFileModificationDate]`
doesExist        = `NSFileManager defaultManager fileExistsAtPath:scriptPath`

execute = {
   `error(scriptPath.." does not exist!")` unless: doesExist()
   `"Reloading..." print`                  unless: task == nil

    lastModified = modificationDate()
    task terminate
    task = NSTask new;
        setLaunchPath: "/usr/local/tranquil/bin/tranquil";
         setArguments: [scriptPath];
    setStandardOutput: NSFileHandle fileHandleWithStandardOutput;
     setStandardError: NSFileHandle fileHandleWithStandardError;
     setStandardInput: NSFileHandle fileHandleWithStandardInput;
               launch;
                 self
}

until forever {
    execute if: (modificationDate() > lastModified) || lastModified == nil
    Usleep(500000)
}
